# Maintainer : Lone_Wolf <lonewolf@klaas-de-kat.nl>
# Contributor : Eric Engestrom <eric@engestrom.ch>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor : Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libdrm-ps4
_pkgbasename=libdrm
pkgver=2.4.109
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services, git 32-bit version"
arch=(x86_64)
license=('custom')
depends=('libdrm-ps4' 'lib32-libpciaccess' 'lib32-gcc-libs')
makedepends=('meson' 'git' 'ninja')
checkdepends=('lib32-cairo')
url="http://dri.freedesktop.org/"
provides=('lib32-libdrm')
conflicts=('lib32-libdrm')

source=(https://dri.freedesktop.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.xz # {,.sig}
        libdrm.patch
        COPYING)
sha512sums=('45194bb28207421afcff3e247930d2eeab77a5fea58e6be22f67525f2e0aab2f410a9ae0ab315b55ef56b80c481bba40b1660468b7cda539758b2bbb8e814fd8'
            SKIP
            SKIP)
          
prepare() {
    cd $_pkgbasename-$pkgver
	patch -Np1 < ../libdrm.patch
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG=/usr/bin/i686-pc-linux-gnu-pkg-config
    meson setup $_pkgbasename-$pkgver _build \
        --prefix /usr \
        --libdir lib32 \
        --buildtype plain \
        --wrap-mode      nofallback \
        -D udev=false \
        -D valgrind=false
    meson configure _build
    ninja -C _build
}

check() {
   # '-t 10' is needed for the 'threaded' test, which uses the default meson
   # test timeout of 30 seconds. This is too short for many systems. It can be
   # removed if upstream fixes the issue.
    meson test -C _build -t 10
}

package() {
    DESTDIR="$pkgdir" ninja -C _build install
  
    # remove files already provided by libdrm-git
    rm -rf "$pkgdir"/usr/{include,share,bin}
  
    install -Dt "$pkgdir/usr/share/licenses/$pkgname"  -m644 COPYING
}
