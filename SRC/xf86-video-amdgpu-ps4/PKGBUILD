# Original Maintainer: Laurent Carlier <lordheavym@gmail.com>
# PS4 Maintainer: Nazky <@NazkyYT>

pkgname=xf86-video-amdgpu-ps4
_pkgname=xf86-video-amdgpu
pkgver=22.0.0
pkgrel=1
pkgdesc="X.org amdgpu video driver PS4 version"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('systemd-libs' 'mesa')
makedepends=('xorg-server-devel' 'systemd' 'X-ABI-VIDEODRV_VERSION=25.2')
conflicts=('xorg-server<1.20.0' 'X-ABI-VIDEODRV_VERSION<25' 'X-ABI-VIDEODRV_VERSION>=26')
provides=('xf86-video-amdgpu')
groups=('xorg-drivers')
source=(${url}/releases/individual/driver/${_pkgname}-${pkgver}.tar.xz
        xf86-video-amdgpu.patch)
sha512sums=(SKIP
            SKIP)

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 < ../xf86-video-amdgpu.patch
}

build() {
  cd ${_pkgname}-${pkgver}

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  #CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  ./configure --prefix=/usr \
    --enable-glamor
  make
}

check() {
  cd ${_pkgname}-${pkgver}
  make check
}

package() {
  cd ${_pkgname}-${pkgver}

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
